// This code utilises the OpenCV - FaceX to Servo module as its basic framework, 
// It was tweaked to use faceProximity instead of faceX for input with help from ChatGPT.
// Juan Sulca rewrote the proximity formula to use faceY value to determine faceHeight, for which he has our deepest gratitude.

import processing.serial.*;
import cc.arduino.*;
import gab.opencv.*;
import processing.video.*;
import java.awt.*;

Arduino arduino; // create an Arduino object to connect to the board
Capture video;
OpenCV opencv;

// Servo variables
int servoPin1 = 2;
int servoPin2 = 3; // Add a second servo pin
int servoAngle_value;
int faceX; // X-coordinate of the face center
int faceY; // Y-coordinate of the face center
// Used for converting from pixels to angles
int minServoAngle = 180;
int maxServoAngle = 0;

int cameraIndex = 1; // default camera index, change this to select different camera

void setup() {
  size(640, 480);  // Window size for video capture
  printArray(Arduino.list());  // List COM-ports

  // List available cameras
  String[] cameras = Capture.list();
  if (cameras.length == 0) {
    println("There are no cameras available.");
    exit();
  } else {
    println("Available cameras:");
    for (int i = 0; i < cameras.length; i++) {
      println(i + ": " + cameras[i]);
    }
  }

  // Open the port that the Arduino is connected to (change this to match your setup)
  arduino = new Arduino(this, Arduino.list()[1], 57600);

  // Set both servo pins as output
  arduino.pinMode(servoPin1, Arduino.SERVO);
  arduino.pinMode(servoPin2, Arduino.SERVO);

  // Setup for OpenCV and video capture
  video = new Capture(this, 640, 480, cameras[cameraIndex]);
  opencv = new OpenCV(this, 640, 480);
  opencv.loadCascade(OpenCV.CASCADE_FRONTALFACE);
  video.start();
}

void draw() {
  opencv.loadImage(video);
  image(video, 0, 0);
  noFill();
  stroke(0, 255, 0);
  strokeWeight(3);
  Rectangle[] faces = opencv.detect();

  if (faces.length > 0) {
    // Assuming only one face is present, get the distance (proximity) of the face to the camera
    float proximity = dist(width / 2, height / 2, faces[0].x + faces[0].width / 2, faces[0].y + faces[0].height / 2);
    faceX = faces[0].x + faces[0].width / 2;
    faceY = faces[0].y + faces[0].height / 2;
    rect(faces[0].x, faces[0].y, faces[0].width, faces[0].height);

    // Convert proximity to a servo angle
    servoAngle_value = round(map(faces[0].height, 10, 2*height/3, minServoAngle, maxServoAngle));

    // Set both servos to the calculated angle
    arduino.servoWrite(servoPin1, servoAngle_value);
    arduino.servoWrite(servoPin2, servoAngle_value);

    // Draw a bright pink vertical line at the face center
    stroke(255, 20, 147); // Bright pink color
    line(faceX, 0, faceX, height);
  }

  // Display face position and servo angle
  text("Face Proximity: " + round(dist(width / 2, height / 2, faceX, faceY)), faceX + 20, faceY);
  text("Servo 1: " + servoAngle_value, faceX + 20, faceY + 30);
  text("Servo 2: " + servoAngle_value, faceX + 20, faceY + 60);
}

void captureEvent(Capture c) {
  c.read();
}
